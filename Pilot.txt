FUNCTION_BLOCK Pilot


VAR_INPUT
 top_vec : REAL;
 bottom_vec : REAL;
END_VAR
VAR_OUTPUT
 direction : REAL;
END_VAR

FUZZIFY top_vec
 TERM low := (-90, 1) (10, 1) (40, 0);
 TERM high := (33, 0) (70, 1) (90, 1);
END_FUZZIFY

FUZZIFY bottom_vec
 TERM low := (-90, 1) (-70, 1) (-30, 0);
 TERM high := (-50, 0) (-5, 1) (90, 1);
END_FUZZIFY

DEFUZZIFY direction
 TERM down := (0.5, 0) (1, 1) (1.5, 0);
 TERM still := (-0.5, 0) (0, 1) (0.5, 0);
 TERM up := (-1.5, 0) (-1, 1) (-0.5, 0);
 METHOD : COG;
 DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
 AND : MIN;
 ACT : MIN;
 ACCU : MAX;
 RULE 1 : IF top_vec IS low AND bottom_vec IS low THEN direction IS down;
 RULE 2 : IF bottom_vec IS high AND top_vec IS high THEN direction IS up;
 RULE 3 : IF bottom_vec IS low AND top_vec IS high THEN direction IS still;
 RULE 4 : IF top_vec IS low AND bottom_vec IS high THEN direction IS still;
END_RULEBLOCK

END_FUNCTION_BLOCK